#!@GUILE@ \
-e main
!#

;;; fossdem.scm -- Read FOSDEM schedule in iCalendar format.

;; Copyright (C) 2017 Artyom V. Poptsov <poptsov.artyom@gmail.com>
;;
;; This program is free software: you can redistribute it and/or
;; modify it under the terms of the GNU General Public License as
;; published by the Free Software Foundation, either version 3 of the
;; License, or (at your option) any later version.
;;
;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of
;; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
;; General Public License for more details.
;;
;; You should have received a copy of the GNU General Public License
;; along with this program.  If not, see <http://www.gnu.org/licenses/>.


;;; Commentary:

;; Read FOSDEM [1] schedule in iCalendar format and print it to stdout in
;; org-mode format.
;;
;; [1] https://fosdem.org/

;;; Code:

(use-modules (ice-9 popen)
             (ice-9 getopt-long)
             (srfi srfi-41)             ; streams
             ;; Guile-ICS
             (ics)
             (ics conv))

;;;


(define %option-spec
  '((help (single-char #\h) (value #f))
    (year (single-char #\y) (value #t))))

;;;

(define (print-schedule year)
  "Print FOSDEM schedule in org-mode format to stdout."
  ;; We have to use 'curl' because 'http-get' from (web client) does
  ;; not work with HTTPS.
  (let* ((command (format #f "curl 'https://fosdem.org/~a/schedule/ical'"
                          year))
         (p       (open-input-pipe command)))
    (stream-for-each ics-object->org-mode (ics->stream p))))

(define (current-year)
  "Get current year."
  (+ 1900 (tm:year (localtime (current-time)))))

(define (print-help-and-exit)
  (display "\
Usage: fosdem [options]

Options:
  --help, -h                 Print this message and exit.
  --year, -y <year>          Set year (current year is used by default.)
")
  (exit 0))


;;; Entry point.

(define (main args)
  (setlocale LC_ALL "")
  (let* ((options      (getopt-long args %option-spec))
         (help-needed? (option-ref options 'help #f))
         (year         (option-ref options 'year (current-year))))
    (when help-needed?
      (print-help-and-exit))
    (print-schedule year)))

;;;

;; Local Variables:
;; Mode: Scheme
;; End:

;;; fosdem.scm ends here.
